#!/usr/bin/env zsh

# Constants
BASE_CONFIG="base.yaml"
CONFIG_DIR="configs"
DOTBOT_DIR="dotbot"
DOTBOT_BIN="bin/dotbot"
PROFILE_PREFIX="profile-"

# Get the directory of the script
BASE_DIR="$(cd "$(dirname "${(%):-%N}")" && pwd)"

# Function to list available profiles and configs
list_profiles_configs() {
    echo "Available profiles:"
    for profile in "${BASE_DIR}/${CONFIG_DIR}/${PROFILE_PREFIX}"*; do
        [[ -f "$profile" ]] && echo "  - $(basename "$profile" | sed "s/^${PROFILE_PREFIX}//")"
    done

    echo -e "\nAvailable configs:"
    for config in "${BASE_DIR}/${CONFIG_DIR}"/*.yaml; do
        [[ $(basename "$config") != "${PROFILE_PREFIX}"* ]] && [[ $(basename "$config") != "$BASE_CONFIG" ]] && echo "  - $(basename "$config" .yaml)"
    done
}

# Function to combine base and specific config files and run Dotbot
run_dotbot() {
    local config="$1"
    echo -e "\nConfigure $config"
    local base_config="${BASE_DIR}/${CONFIG_DIR}/${BASE_CONFIG}"
    local config_file="${BASE_DIR}/${CONFIG_DIR}/${config}.yaml"
    local combined_config

    combined_config=$(mktemp)
    cat "$base_config" "$config_file" > "$combined_config"
    "${BASE_DIR}/${DOTBOT_DIR}/${DOTBOT_BIN}" -d "${BASE_DIR}" -c "$combined_config"
    rm -f "$combined_config"
}

# Main logic
if [[ "$1" == "--list" ]]; then
    list_profiles_configs
    exit 0
fi

cd "${BASE_DIR}"

# Sync submodule URLs
git submodule sync --quiet --recursive

# Initialize and update submodules in parallel
git submodule update --init --recursive --remote --jobs 8

# Fetch and pull the latest changes for submodules in parallel
# git submodule foreach --recursive 'git pull --quiet &'
# wait

if [[ -f "${BASE_DIR}/${CONFIG_DIR}/${PROFILE_PREFIX}$1" ]]; then
    # Profile mode
    PROFILE_FILE="${BASE_DIR}/${CONFIG_DIR}/${PROFILE_PREFIX}$1"
    shift
    CONFIGS=("${(@f)$(<"$PROFILE_FILE")}")
    for config in "${CONFIGS[@]}" "$@"; do
        run_dotbot "$config"
    done
else
    # Standalone mode
    for config in "$@"; do
        run_dotbot "$config"
    done
fi

# vim: ft=zsh
